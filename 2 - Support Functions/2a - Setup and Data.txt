/*
================================================================================
== SETUP =======================================================================
*/

@create Stat Functions Prototype <sfp>
@set Stat Functions Prototype <sfp>=inherit safe

@fo me=&d.sfp me=[search( name=stat functions prototype <sfp> )]

@fo me=@parent Stat Functions Prototype <sfp>=search( name=Code Object Data Parent <codp> )

/*
================================================================================
== DATA ========================================================================

Register the Data Dictionary and the Data Tags with the SFP.

Register the SFP with the Data Dictionary.

*/

@fo me=&d.data-dictionary [v( d.sfp )]=search( name=Data Dictionary <dd> )
@fo me=&d.data-tags [v( d.sfp )]=search( name=Data Tags <d:t> )
// @fo me=&d.cg [v( d.sfp )]=search( name=GMC Chargen <cg> )

@fo me=&d.sfp [v( d.dd )]=search( name=Stat Functions Prototype <sfp> )


/*
================================================================================
== DOT-FUNCTIONS ===============================================================

Dot-functions are things that are to be used often enough that they could be 
user-defined functions (and often are).

--------------------------------------------------------------------------------
-- Dot-Function: Grab Exact ----------------------------------------------------

	u( .grabexact, <list>, <item>[, <delim>] )

Will grab the exact match of '<item>'. If not found, it will return the first 
alphanumeric match of '<item>*'.

Using wildcards (*, ?) in '<item>' defeats the purpose; use 'grab' instead.
	
*/

&.grabexact [v( d.sfp )]=
	localize( 
		if( 
			t( setr( m, grab( %0, %1, %2, %2 ))), 
			%qm, 
			grab( sort( %0, ?, %2, %2 ), %1*, %2 )
		)
	)

/* 
--------------------------------------------------------------------------------
-- Dot-Function: Crumple -------------------------------------------------------

	u( .crumple, <list>, <delim> )

Removes all but one instance of <delim> inside <list>, and all outside.

*/

&.crumple [v( d.sfp )]=trim( squish( %0, %1 ), b, %1 )

/*
--------------------------------------------------------------------------------
-- Dot-Function: Pmatch --------------------------------------------------------

	u( .pmatch, <target>[, <check offline?>] )

A quasi-advanced form of pmatch() because it includes matching 'me'. 

Set '<check offline?>' to true (or '1' or almost anything really) to check 
offline character objects as well. (My pmatch+ and Glitch's pmatch++ code.)


*/

&.pmatch [v( d.sfp )]=
	localize( strcat( 
		setq( p, 
			if( strmatch( %0, me ), 
				%#, 
				objeval( %#, pmatch( %0 ))
			)
		), 
		if( cor( t( %qp ), not( t( %1 ))), 
			%qp, 
			first( search( eplayer=strmatch( name( ## ), %0* )))
		)
	))


/*
================================================================================
== SEARCH ORDER ================================================================

d.search-order: 
	When name matching, go through the various stat types in this order. 
	Therefore, "str" will match "strength" before "strength of the lion".

	This is technically now a function, but for historical reasons we'll 
	leave it as a data collection.

d.search-order-<##>-<gameline>:
	As long as ## is between 2 and 8, search in that order.
	`02` should be for powers
	`03` should be for less important additions, such as renown
	`04` should be for smaller, social additions, such as entitlements
	`09` should be for the least important traits, such as full name

e.g.: 
	core: (01) attribute skill merit advantage (09) bio
	vampire: (02) ... (03) ...
	demon: (02) embed exploit (03) demonic_form
	werewolf: (02) gift rite (03) renown

d.type.specials:
	These are stat classes that are not rollable nor settable via softcode.

--------------------------------------------------------------------------------
*/

&d.search-order [v( d.sfp )]=
	iter( sort( lattr( %!/d.search-order-* )), v( %i0 ))
&d.search-order-01 [v( d.sfp )]=attribute skill merit advantage
// &d.search-order-<##>-<gameline> [v( d.sfp )]=<stuff>
&d.search-order-09 [v( d.sfp )]=bio

&d.type.specials [v( d.sfp )]=special health 

&filter.search-types [v( d.sfp )]=
	t( match( u( d.search-order ), first( %0, . )))

&sortby.types [v( d.sfp )]=
	[setq( o, u( d.search-order ))]
	[comp( match( %qo, first( %0, . )), match( %qo, first( %1, . )))]

// -----------------------------------------------------------------------------
// -- test --

think sortby( v( d.sfp )/sortby.types, filter( v( d.sfp )/filter.search-types, sort( lattr( v( d.dd )/*.s* ))))

think sortby( v( d.sfp )/sortby.types, filter( v( d.sfp )/filter.search-types, sort( lattr( v( d.dd )/*.str* ))))
